// This is super handy
// https://github.com/spring-projects/gradle-plugins/tree/master/propdeps-plugin

buildscript {
    repositories {
        maven { url 'https://repo.spring.io/plugins-release' }
    }
    dependencies {
        classpath 'org.springframework.build.gradle:propdeps-plugin:0.0.7'
    }
}

configure(allprojects) {
    apply plugin: 'propdeps'
    apply plugin: 'propdeps-maven'
    apply plugin: 'propdeps-idea'
}

allprojects {
  apply plugin: 'idea'
}

subprojects {
  apply plugin: 'java'
  apply plugin: 'scala'
  group = 'org.openrepose.prototype'
  version = '1.NOPE'

  repositories {
    mavenCentral()
    maven { url "http://repo.spring.io/release" }
  }

  dependencies {
    compile "org.scala-lang:scala-library:2.10.4"
  }

    tasks.withType(ScalaCompile) {
        scalaCompileOptions.useAnt = false
    }

    task 'create-dirs' << {
        sourceSets.all { set ->
            set.allSource.srcDirs.each {it.mkdirs()}
        }
    }

}

//Core IMPL depends on the API
project(':core-impl') {
  apply plugin: "application"

    mainClassName = "org.protorepose.valve.Main"

  dependencies {
    runtime project(':core-api'), "org.springframework:spring-context:4.1.0.RELEASE", 'org.eclipse.jetty:jetty-server:9.2.2.v20140723'
  }
}

project(":core-api") {
    dependencies {
        provided "org.springframework:spring-context:4.1.0.RELEASE"
    }
}

//Service impl needs the service API, and the Core API
project(':service-impl') {
  dependencies {
    compile project(':service-api'), project(':core-api')
  }
}

// A "filter" needs the core api, and a service-api
project(':filter') {
  dependencies {
    compile project(':core-api'), project(':service-api')
  }
}

